//WSBGWVOL JOB  ,GOLOB,MSGCLASS=X,NOTIFY=WSBG
//S5       EXEC  ASMACL MAC='STDL.MVS.AMODGEN'
//ASM.SYSIN  DD  *
         TITLE 'WVOLCOLN - E35 EXIT TO FIX UP RECORDS'
***********************************************************************
*                                                                     *
*                                                                     *
*    WRITTEN BY JOHN R. GROUT                                         *
*                                                                     *
*    MODULE:   WVOLCOLN                                               *
*                                                                     *
*    FUNCTION: WVOLCOLN IS CALLED FOR EACH RECORD LEAVING THE         *
*              FINAL SORT: IT DELETES DUPLICATE CONTROL RECORDS AND   *
*              FIXES AUTO EXCLUDED DATA SET RECORDS                   *
*                                                                     *
*    INPUT:    DATA SET RECORD                                        *
*                                                                     *
*    OUTPUT:   RETURN CODE IN REG 15                                  *
*                 0 - ACCEPT RECORD (POINTED TO BY REG 1)             *
*                 4 - DELETE RECORD (WHEN DUP CTL REC READ)           *
*                 8 - DO NOT RETURN (AT END OF FILE)                  *
*                                                                     *
*    REGISTERS:                                                       *
*                 R12 - BASE REGISTER                                 *
*                                                                     *
*    EXTERNAL REFERENCES:                                             *
*                                                                     *
*    MACROS USED:                                                     *
*                                                                     *
*    NONE                                                             *
*                                                                     *
* PROGRAM LOGIC:                                                      *
*                                                                     *
* SEE ABOVE.                                                          *
*                                                                     *
* INPUT:                                                              *
*                                                                     *
* SEE ABOVE.                                                          *
*                                                                     *
* OUTPUT:                                                             *
*                                                                     *
* SEE ABOVE.                                                          *
***********************************************************************
          EJECT
***********************************************************************
*                                                                     *
* CONTROL RECORD                                                      *
* ******* ******                                                      *
*                                                                     *
* A RECORD IS DESCRIBED BY DSECT CTLREC                               *
*                                                                     *
*   BYTES 0-1        RECORD DESCRIPTOR WORD (SET TO 150 BYTES)        *
*                                                                     *
*   BYTES 2-3        RESERVED (MUST BE ZERO)                          *
*                                                                     *
*   BYTES 4-5        DATA SET NUMBER (SET TO ZERO)                    *
*                                                                     *
*   BYTES 6-7        MEMBER NUMBER (SET TO ZERO)                      *
*                                                                     *
*   BYTE  8          RECORD TYPE:                                     *
*                                                                     *
*                      X'00' - CONTROL RECORD                         *
*                                                                     *
*   BYTE  9          SECTION TYPE                                     *
*                                                                     *
*                      X'00' - BASE                                   *
*                                                                     *
*   BYTES 10-15      VOLUME SERIAL                                    *
*                                                                     *
*   BYTES 16-149     RESERVED (SET TO ZERO)                           *
*                                                                     *
* DATA SET RECORDS                                                    *
* **** *** *******                                                    *
*                                                                     *
* A RECORD IS DESCRIBED BY DSECT CDSREC                               *
*                                                                     *
*   CDSREC                                                            *
*                                                                     *
*   BYTES 0-1        RECORD DESCRIPTOR WORD (SET TO 150 BYTES)        *
*                                                                     *
*   BYTES 2-3        RESERVED (MUST BE ZERO)                          *
*                                                                     *
*   BYTES 4-5        DATA SET NUMBER (SET TO ZERO)                    *
*                                                                     *
*   BYTES 6-7        MEMBER NUMBER (SET TO ZERO)                      *
*                                                                     *
*   BYTE  8          RECORD TYPE:                                     *
*                                                                     *
*                      X'01' - DATA SET RECORD                        *
*                                                                     *
*   BYTE  9          SECTION TYPE:                                    *
*                                                                     *
*                      X'00' - SELECTABLE LOAD PDS                    *
*                      X'01' - SELECTABLE SOURCE PDS                  *
*                      X'02' - OTHER NON-VSAM DATA SET                *
*                                                                     *
*   BYTES 10-149     FORMAT-1 DSCB (140 BYTES)                        *
***********************************************************************
         TITLE 'WVOLCOLN - MAPPING DSECTS'
*
* COMPARISON DATASET FORMATS
*
* CONTROL RECORD
CTLREC   DSECT
CTLLEN   DS    H                     LENGTH OF RECORD
CTLRESV  DS    CL8                   RESERVED (MUST BE ZERO)
         ORG   CTLRESV+2
CTLDSNM  DS    H                     DATA SET NUMBER
CTLMBNM  DS    H                     MEMBER NUMBER
CTLTYPE  DS    XL1                   RECORD TYPE
TYPECTL  EQU   X'00'                 CONTROL RECORD
CTLSTYP  DS    XL1                   SECTION TYPE
CTLVOLSR DS    CL6                   VOLUME SERIAL
CTLRESV1 DS    CL134                 RESERVED (MUST BE ZERO)
* DATASET RECORD
CDSREC   DSECT
CDSLEN   DS    H                     LENGTH OF RECORD
CDSRESV  DS    H                     RESERVED (MUST BE ZERO)
CDSDSNM  DS    H                     DATA SET NUMBER
CDSMBNM  DS    H                     MEMBER NUMBER
CDSTYPE  DS    XL1                   RECORD TYPE
TYPEDSR  EQU   X'01'                 DATA SET RECORD
CDSSTYPE DS    XL1                   SECTION TYPE
STYPLOAD EQU   X'00'                 LOAD PDS (SELECTABLE)
STYPSRCE EQU   X'01'                 SOURCE PDS (SELECTABLE)
STYPNSLC EQU   X'02'                 NON-SELECTABLE
STYPEXCL EQU   X'80'                 AUTOMATIC EXCLUDE FLAG
CDSFMT1D DS    CL140                 FORMAT-1 DSCB
         ORG   CDSFMT1D
CDSDSN   DS    CL44                  DATA SET NAME
         SPACE  ,
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU    10
R11      EQU    11
R12      EQU    12
R13      EQU    13
R14      EQU    14
R15      EQU    15
         TITLE  'WVOLCOLN -- MAIN PROGRAM'
WVOLCOLN CSECT
         SAVE  (11,12)
         SPACE ,
         LR   R12,R15
         USING WVOLCOLN,R12
         SPACE ,
         L     R11,0(,R1)           LOAD POINTER TO RECORD
         LTR   R11,R11              IS POINTER ZERO?
         BZ    COLNNRET             YES, DO NOT RETURN
         USING CTLREC,R11
         CLI   CTLTYPE,TYPECTL      IS THIS A CONTROL RECORD?
         BNE   COLNDSR              NO, GO HANDLE DATA SET RECORD
         DROP  R11
         TM    FLAGS,CTLREAD        HAS A CTL RECORD BEEN READ BEFORE?
         BO    DELREC               YES, DELETE THIS ONE
         OI    FLAGS,CTLREAD        A CONTROL RECORD HAS NOW BEEN READ
         B     RETREC
COLNDSR  EQU   *
         USING CDSREC,R11
         CLI   CDSTYPE,TYPEDSR      IS THIS A DATA SET RECORD?
         BNE   RETREC               NO - GO ACCEPT RECORD
         TM    CDSSTYPE,STYPEXCL    IS THIS RECORD AUTO EXCLUDED?
         BZ    RETREC               NO - GO ACCEPT RECORD
         DROP  R11
         LA    R1,WORKAREA
         MVC   0(150,R1),0(R11)     MOVE RECORD TO WORKAREA
         USING CDSREC,R1
         NI    CDSSTYPE,X'FF'-STYPEXCL TURN OFF AUTO EXCLUDE
         B     RETREC1              GO ACCEPT RECORD
         DROP  R1
DELREC   LA    R15,4
         B     COLNEXIT
COLNNRET LA    R15,8                DO NOT RETURN
         B     COLNEXIT             GO EXIT
RETREC   LR    R1,R11               POINT TO RECORD TO ACCEPT
RETREC1  SLR   R15,R15              INSERT RECORD
COLNEXIT RETURN (11,12)
FLAGS    DC    XL1'00'
CTLREAD  EQU   X'80'
WORKAREA DS    CL150
         END
//LKED.SYSLMOD  DD  DISP=SHR,DSN=WSBG.WVOL.LOADLIB(WVOLCOLN)
