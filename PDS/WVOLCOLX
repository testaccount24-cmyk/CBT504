//WSBGWVOL JOB  ,GOLOB,MSGCLASS=X,NOTIFY=WSBG,CLASS=S
//S1       EXEC  ASMACL MAC='STDL.MVS.AMODGEN'
//ASM.SYSIN  DD  *
         TITLE 'WVOLCOLM - E15 EXIT TO PASS DATA SET RECORDS'
***********************************************************************
*                                                                     *
*                                                                     *
*    ADAPTED FROM WVOLCOLM WRITTEN BY JOHN GROUT                      *
*                                                                     *
*    MODULE:   WVOLCOLX                                               *
*                                                                     *
*    FUNCTION: WVOLCOLX IS CALLED TO CREATE EACH RECORD FOR THE       *
*              DATA SET RECORD ALPHABETIC SORT: IT COPIES CONTROL     *
*              RECORDS AND NUMBERS DATA SET RECORDS.                  *
*                                                                     *
*    INPUT:    STG1DS0 - MAIN INPUT RECORDS (FOR THIS SITE)           *
*                                                                     *
*    OUTPUT:   RETURN CODE IN REG 15                                  *
*                 8 - DO NOT RETURN (WHEN NO MORE RECORDS)            *
*                12 - INSERT RECORD (POINTED TO BY R1)                *
*                16 - ERROR (WHEN DATA SETS ARE BROKEN OR MISSING)    *
*                                                                     *
*    REGISTERS:                                                       *
*                 R12 - BASE REGISTER                                 *
*                                                                     *
*    EXTERNAL REFERENCES:                                             *
*                                                                     *
*    MACROS USED:                                                     *
*                                                                     *
*    NONE                                                             *
*                                                                     *
* PROGRAM LOGIC:                                                      *
*                                                                     *
* SEE ABOVE.                                                          *
*                                                                     *
* INPUT:                                                              *
*                                                                     *
* SEE ABOVE.                                                          *
*                                                                     *
* OUTPUT:                                                             *
*                                                                     *
* SEE ABOVE.                                                          *
***********************************************************************
          EJECT
***********************************************************************
*                                                                     *
* DATA SET RECORDS                                                    *
* **** *** *******                                                    *
*                                                                     *
* A RECORD IS DESCRIBED BY DSECT CDSREC                               *
*                                                                     *
*   CDSREC                                                            *
*                                                                     *
*   BYTES 0-1        RECORD DESCRIPTOR WORD (SET TO 150 BYTES)        *
*                                                                     *
*   BYTES 2-3        RESERVED (MUST BE ZERO)                          *
*                                                                     *
*   BYTES 4-5        DATA SET NUMBER (SET TO ZERO)                    *
*                                                                     *
*   BYTES 6-7        MEMBER NUMBER (SET TO ZERO)                      *
*                                                                     *
*   BYTE  8          RECORD TYPE:                                     *
*                                                                     *
*                      X'01' - DATA SET RECORD                        *
*                                                                     *
*   BYTE  9          SECTION TYPE:                                    *
*                                                                     *
*                      X'00' - SELECTABLE LOAD PDS                    *
*                      X'01' - SELECTABLE SOURCE PDS                  *
*                      X'02' - OTHER NON-VSAM DATA SET                *
*                                                                     *
*   BYTES 10-149     FORMAT-1 DSCB (140 BYTES)                        *
***********************************************************************
         TITLE 'WVOLCOLX - MAPPING DSECTS'
*
* COMPARISON DATASET FORMATS
*
* DATASET RECORD
CDSREC   DSECT
CDSLEN   DS    H                     LENGTH OF RECORD
CDSRESV  DS    H                     RESERVED (MUST BE ZERO)
CDSDSNM  DS    H                     DATA SET NUMBER
CDSMBNM  DS    H                     MEMBER NUMBER
CDSTYPE  DS    XL1                   RECORD TYPE
TYPEDSR  EQU   X'01'                 DATA SET RECORD
CDSSTYPE DS    XL1                   SECTION TYPE
STYPLOAD EQU   X'00'                 LOAD PDS (SELECTABLE)
STYPSRCE EQU   X'01'                 SOURCE PDS (SELECTABLE)
STYPNSLC EQU   X'02'                 NON-SELECTABLE
STYPEXCL EQU   X'80'                 AUTOMATIC EXCLUDE FLAG
CDSFMT1D DS    CL140                 FORMAT-1 DSCB
         ORG   CDSFMT1D
CDSDSN   DS    CL44                  DATA SET NAME
         DCBD  DEVD=DA
         SPACE  ,
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU    10
R11      EQU    11
R12      EQU    12
R13      EQU    13
R14      EQU    14
R15      EQU    15
         TITLE  'WVOLCOLM -- MAIN PROGRAM'
WVOLCOLM CSECT
         SAVE  (14,12)
         SPACE ,
         LR   R12,R15
         USING WVOLCOLM,R12
         ST    R13,SAVEAREA+4
         LA    R1,SAVEAREA
         ST    R1,8(,R13)
         LR    R13,R1
         SPACE ,
         TM    MODE,INIT            HAVE WE GONE THROUGH INITIALIZATION
         BO    GETS00               YES, SKIP INITIALIZATION
         LA    R1,OPENDCB           POINT TO DCB LIST
         OPEN  MF=(E,(1))           OPEN DCBS FOR PROCESSING
         USING IHADCB,R3
         LA    R3,STG1DS0           POINT TO STG1DS0 DCB
         TM    DCBOFLGS,X'10'       DID DATA SET OPEN?
         BZ    BADDCB               NO - GO EXIT
         OI    MODE,INIT            WE HAVE GONE THROUGH INIT PROCESS
GETS00   GET   STG1DS0              GET A RECORD FROM STG1DS0
         LR    R9,R1                RECORD IS POINTED TO BY R9
         USING CDSREC,R9
         CLI   CDSTYPE,TYPEDSR      IS THIS A CONTROL RECORD?
         BNE   DOREC                YES, GO RETURN CONTROL RECORD
         LH    R1,DSNCTR            INCREMENT DATA SET COUNTER
         AH    R1,=H'1'
         STH   R1,DSNCTR
         MVC   CDSDSNM(2),DSNCTR    MOVE DATA SET COUNTER INTO RECORD
DOREC    LR    R1,R9                POINT TO INSERTED RECORD
         LA    R15,12               INSERT RECORD
         L     R13,4(,R13)          POINT TO CALLERS SAVE AREA
         L     R14,12(,R13)         LOAD REGISTER 14
         L     R0,20(,R13)          LOAD REGISTER 0
         RETURN (2,12),RC=(15)      RETURN REST OF REGS AND EXIT
STG1DS0E LA    R1,OPENDCB           POINT TO DCB LIST
         CLOSE MF=(E,(1))           CLOSE DATA SETS
         LA    R15,8                DO NOT RETURN TO EXIT
COLMEXIT L     R13,4(,R13)          POINT TO CALLERS SAVE AREA
         RETURN (14,12),RC=(15)     RETURN TO CALLER
BADDCB   LA    R15,16               SIGNAL ERROR IN SORT
         B     COLMEXIT
SAVEAREA DS    18F
STG1DS0  DCB   DDNAME=STG1DS0,                                         X
               DSORG=PS,                                               X
               LRECL=264,                                              X
               EODAD=STG1DS0E,                                         X
               RECFM=VB,                                               X
               MACRF=(GL)
OPENDCB  OPEN  (STG1DS0,(INPUT)),MF=L
DMYDSREC DS    0F
         DC    H'48'
         DC    H'0'
         DC    44XL1'FF'
DSNCTR   DC    H'-1'
MODE     DC    X'00'
INIT     EQU   X'80'                INITIALIZATION PERFORMED
         END
//*KED.SYSLMOD  DD  DISP=SHR,DSN=WSBG.WVOL.LOADLIB(WVOLCOLX)
//LKED.SYSLMOD  DD  DISP=SHR,DSN=WSBG.LOAD(WVOLCOLX)
