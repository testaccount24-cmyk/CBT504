OPTIONS PAGESIZE=80  SORTNAME=SYNCSORT SORTWKNO=1;
PROC CONTENTS DATA=MEMBER._ALL_;
PROC FORMAT;
  VALUE MSG
    0   = 'SAME AS NEXT MEMBER     '
    1   = 'DE MISMATCH             '
    2   = 'DATE MISMATCH           '
    3   = 'DATE & DE MISMATCH      '
    4   = 'HASH MISMATCH           '
    5   = 'HASH & DE MISMATCH      '
    6   = 'HASH & DATE MISMATCH    '
    7   = 'HASH, DATE & DE MISMATCH'
   15   = 'LAST MEMBER             '
  OTHER = 'OOPS                    ';

    %INCLUDE CNTLLIB3;

/*DATA ALLDATA;
     SET  MEMBER.SSLIB1
     MEMBER.SSLIB3
     MEMBER.SSLIB4
     MEMBER.SSLIB5
     MEMBER.SSRES1 ;  */

  DATA ALLDATA;
     SET  MEMBER.ALLDATA ;

DATA TLIB
     XLIB;
  SET /*MEMBER.SSLIB1
      MEMBER.SSLIB3
      MEMBER.SSLIB4
      MEMBER.SSLIB5
      MEMBER.SSRES1*/ ALLDATA
        END=EOD;
      BY DSN NOTSORTED;
    IF MEMBER ¬= '$$$COIBM';
    IF MEMBER ¬= '$$$INDEX';
    IF TYPE    = 'LL' ;
    IF CLASS = 'TLIB' THEN OUTPUT TLIB;
    IF CLASS = 'XLIB' THEN DO;
      ORDER = PUT(DSN,$LLIST.);
      OUTPUT XLIB;
      END;

PROC SORT DATA=TLIB;
  BY MEMBER DATE DE;

DATA TLIBCMPR;
  LENGTH DSNT1-DSNT5 $44;
  ARRAY DSNT DSNT1-DSNT5;
  DO I=1 TO 5;
    SET TLIB END=LAST;
      BY MEMBER DATE DE;
    DSNT{I} = DSN;
    IF LAST.DE THEN DO;
      OUTPUT TLIBCMPR;
      RETURN;
      END;
    END;

PROC SORT DATA=XLIB;
  BY MEMBER DATE DE ORDER;

DATA XLIBCMPR;
  LENGTH DSNX1-DSNX5 $44;
  ARRAY DSNX DSNX1-DSNX5;
  DO I=1 TO 5;
    SET XLIB END=LAST;
      BY MEMBER DATE DE;
    DSNX{I} = DSN;
    IF LAST.DE THEN DO;
      OUTPUT XLIBCMPR;
      RETURN;
      END;
    END;

DATA LLISTDUP;
  MERGE TLIBCMPR
        XLIB(IN=XLIB_IN);
    BY MEMBER DATE DE;
    IF XLIB_IN AND ORDER NE '99';

PROC SORT DATA=LLISTDUP;
  BY MEMBER ORDER;

DATA LLISTDUP;
  SET LLISTDUP;
    BY MEMBER;
    IF FIRST.MEMBER AND LAST.MEMBER AND DSNT2 = ' ' THEN DELETE;

TITLE1 'LOAD MODULE INVENTORY - XLIB TO TLIB CROSS REFRENCE';
TITLE2 'DUPLICATE LPA AND LINKLIST MODULES ONLY';
PROC PRINTTO PRINT=LLIST;
PROC PRINT DATA=LLISTDUP;
  BY MEMBER;
  ID MEMBER;
  VAR DSN DATE DSNT1 DSNT2 DSNT3;
  FORMAT DATE MMDDYY8.;
PROC PRINTTO;

DATA MATCH(DROP=DSN)
     NOMATCH(DROP=FLAG1);
  MERGE XLIBCMPR(IN=XLIB_IN)
        TLIBCMPR(IN=TLIB_IN);
    BY MEMBER DATE DE;
  FLAG1=0;
  IF TLIB_IN + XLIB_IN NE 2 THEN OUTPUT NOMATCH;
    ELSE DO;
      IF DSNT2 NE ' ' THEN FLAG1 + 1;
      IF DSNX2 NE ' ' THEN FLAG1 + 2;
      OUTPUT MATCH;
      END;

PROC SORT DATA=MATCH EQUALS;
  BY FMID MEMBER DATE;

TITLE1 'LOAD MODULE INVENTORY - TLIB TO XLIB CROSS REFERENCE';
TITLE2 'BY PRODUCT';
DATA _NULL_;
  ARRAY DSNT $44 DSNT1-DSNT5;
  ARRAY DSNX $44 DSNX1-DSNX5;
  SET MATCH;
    BY FMID MEMBER DATE;
 /* %INCLUDE CNTLLIB1 ; */
    FILE PRINT LINESLEFT=REMAIN PAGESIZE=66;
    IF _N_=1 THEN LINK HEADER;
    IF REMAIN LE 3 THEN LINK HEADER;
    IF FIRST.FMID   THEN PUT / @1  FMID      $8. @;
    IF FIRST.MEMBER THEN PUT @9  MEMBER    $8. @;
    IF FIRST.DATE   THEN PUT @18 DATE   MMDDYY. @;
    PUT @30 DSNT{1} $44. @80 DSNX{1} $44.;
    DO I=2 TO 5;
      IF DSNT{I} NE ' ' OR DSNX{I} NE ' ' THEN
        PUT @30 DSNT{I} $44. @80 DSNX{I} $44.;
      END;
RETURN;

HEADER:
  IF _N_ NE 1 THEN PUT _PAGE_;
  PUT / @1 'PRODUCT' @10 'MEMBER' @18 'LKED DATE' @30 'TLIB' @80 'XLIB';
  IF FIRST.FMID=0 THEN PUT / @1FMID $8. @;
RETURN;

DATA TLIB;
  ARRAY DSNT $44 DSNT1-DSNT5;
  ARRAY DSNX $44 DSNX1-DSNX5;
  SET MATCH;
  DO I=1 TO 5;
    TLIB = DSNT{I};
    DO J=1 TO 5;
      XLIB = DSNX{J};
      IF TLIB NE ' ' AND XLIB NE ' ' THEN OUTPUT TLIB;
      END;
    END;

PROC SORT DATA=TLIB;
  BY TLIB MEMBER;

TITLE1 'LOAD MODULE INVENTORY - TLIB TO XLIB CROSS REFRENCE';
TITLE2 'BY TLIB';
DATA _NULL_;
  SET TLIB;
  BY TLIB MEMBER;
 /* %INCLUDE CNTLLIB2; */
  FILE TLIB PS=80 LS=80 LINESLEFT=REMAIN;
  IF FIRST.TLIB OR REMAIN LE 3 THEN LINK HEADER;
  IF FIRST.MEMBER THEN PUT @3 MEMBER $8. @;
  PUT               @13 XLIB $20. @;
  IF FLAG1 = '.......1'B THEN
    PUT @35 'MEMBER EXISTS IN MULTIPLE TLIBS';
  ELSE PUT;
RETURN;

HEADER:
  IF REMAIN LE 3 THEN PUT _PAGE_;
  PUT / @1 '***** ' TLIB : $44. '- ' FMID $8.;
  PUT / @3 'MEMBER' @13 'XLIB' @35 'COMMENT';
RETURN;

DATA XLIB;
  ARRAY DSNT $44 DSNT1-DSNT5;
  ARRAY DSNX $44 DSNX1-DSNX5;
  SET MATCH;
  DO I=1 TO 5;
    XLIB = DSNX{I};
    DO J=1 TO 5;
      TLIB = DSNT{J};
      IF XLIB NE ' ' AND TLIB NE ' ' THEN OUTPUT XLIB;
      END;
    END;

PROC SORT DATA=XLIB;
  BY XLIB MEMBER;

TITLE1 'LOAD MODULE INVENTORY - XLIB TO TLIB CROSS REFRENCE';
TITLE2 'BY XLIB';
DATA _NULL_;
  SET XLIB;
  BY XLIB MEMBER;
 /* %INCLUDE CNTLLIB2; */
  FILE XLIB PS=80 LS=80 LINESLEFT=REMAIN;
  IF FIRST.XLIB OR REMAIN LE 3 THEN LINK HEADER;
  IF FIRST.MEMBER THEN PUT @3 MEMBER $8. @;
  PUT               @13 TLIB $30. @;
  IF FLAG1 = '......1.'B THEN
    PUT @45 'MEMBER EXISTS IN MULTIPLE XLIBS';
  ELSE PUT;
RETURN;

HEADER:
  IF REMAIN LE 3 THEN PUT _PAGE_;
  PUT / @1 '***** ' XLIB : $44. ;
  PUT / @3 'MEMBER' @13 'TLIB' @45 'COMMENT';
RETURN;

PROC SORT DATA=XLIB;
  BY MEMBER ORDER;

PROC CONTENTS DATA=MATCH;

PROC CONTENTS DATA=NOMATCH;

TITLE1 'LOAD MODULE INVENTORY - XLIB TO TLIB CROSS REFRENCE';
TITLE2 'NOMATCH REPORT';

PROC PRINTTO PRINT=NOMATCH;
PROC PRINT DATA=NOMATCH;
  WHERE CLASS='XLIB';
  VAR DSN MEMBER DATE FMID DSNX1;
  FORMAT DATE DATE8.;
PROC PRINTTO;

TITLE1 'LOAD MODULE INVENTORY - XLIB TO TLIB CROSS REFRENCE';
TITLE2 'MATCH REPORT';

PROC PRINTTO PRINT=MATCH;
PROC PRINT DATA=MATCH;
  BY FMID MEMBER DATE;
  ID FMID MEMBER DATE;
  VAR DSNT1-DSNT2 DSNX1-DSNX2;
  FORMAT DATE JULIAN.;
PROC PRINTTO;

